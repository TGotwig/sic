WHITESPACE = _{ " "+ }
sep = _{ ";" }

fp = @{ int ~ ("." ~ ASCII_DIGIT+)? }
uint = @{ ASCII_DIGIT+ }
int  = @{ "-"? ~ ASCII_DIGIT+ }
string_ascii = @{ ASCII_ALPHANUMERIC+ }

// string_unicode is based on https://pest.rs/book/examples/json.html
string_unicode = ${ quotation ~ string_inner ~ quotation }
string_inner = @{ char* }
quotation = _{ "'" | "\"" }
char = {
    !quotation ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

triplet_sep = _{ WHITESPACE ~ "|" ~ WHITESPACE }
triplet_fp3 = _{ fp ~ WHITESPACE ~ fp ~ WHITESPACE ~ fp }

f3x3_args_sep = _{ triplet_fp3 ~ triplet_sep ~ triplet_fp3 ~ triplet_sep ~ triplet_fp3 }
f3x3_args_no_sep = _{ triplet_fp3 ~ WHITESPACE ~ triplet_fp3 ~ WHITESPACE ~ triplet_fp3 }

blur = ${ ^"blur" ~ WHITESPACE ~ fp }
brighten = ${ ^"brighten" ~ WHITESPACE ~ int }
contrast = ${ ^"contrast" ~ WHITESPACE ~ fp }
crop = ${ ^"crop" ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint }
diff = ${ ^"diff" ~ WHITESPACE ~ string_unicode }
filter3x3 = ${ ^"filter3x3" ~ WHITESPACE ~ (f3x3_args_sep | f3x3_args_no_sep) }
flip_horizontal = { ^"flip-horizontal" }
flip_vertical = { ^"flip-vertical"  }
grayscale = { ^"grayscale" }
huerotate = ${ ^"hue-rotate" ~ WHITESPACE ~ int }
invert = { ^"invert" }
resize = ${ ^"resize" ~ WHITESPACE ~ uint ~ WHITESPACE ~ uint }
rotate90 = { ^"rotate90" }
rotate180 = { ^"rotate180" }
rotate270 = { ^"rotate270" }
unsharpen = ${ ^"unsharpen" ~ WHITESPACE ~ fp ~ WHITESPACE ~ int }

env_resize_sampling_filter_name = ${^"resize" ~ WHITESPACE ~ ^"sampling_filter"}
env_resize_preserve_aspect_ratio_name = ${^"resize" ~ WHITESPACE ~ ^"preserve_aspect_ratio"}

env_available = _{
      env_resize_sampling_filter_name
    | env_resize_preserve_aspect_ratio_name
}

set_resize_sampling_filter = ${env_resize_sampling_filter_name ~ WHITESPACE ~ string_ascii }
set_resize_preserve_aspect_ratio = ${ env_resize_preserve_aspect_ratio_name }

setenv_available = _{
	  set_resize_sampling_filter
    | set_resize_preserve_aspect_ratio
}

setopt = ${^"set" ~ WHITESPACE ~ setenv_available}
unsetopt = ${^"del" ~ WHITESPACE ~ env_available}

operation = _{
      blur
    | brighten
    | contrast
    | crop
    | diff
    | filter3x3
    | flip_horizontal
    | flip_vertical
    | grayscale
    | huerotate
    | invert
    | resize
    | rotate90
    | rotate180
    | rotate270
    | unsharpen
}

sequence = _{
	(operation | setopt | unsetopt ) ~ ( (NEWLINE* ~ EOI) | (sep ~ NEWLINE+) | sep)
}

main = _{ SOI ~ sequence* ~ EOI }
