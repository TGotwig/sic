whitespace = _{ (" " | "\t" | "\n" | "\r\n") }
sep = _{ ";" }


fp = @{ int ~ ("." ~ digit+)? }
uint = @{ digit+ }
int  = @{ "-"? ~ digit+ }

digit = { '0'..'9' }

operation_sequence = _{ (operation ~ sep?)+ }

operation = _{ blur | brighten | contrast | flip_horizontal | flip_vertical | grayscale | huerotate | invert | resize | rotate90 | rotate180 | rotate270 }

blur = ${ ^"blur" ~ whitespace ~ uint}
brighten = ${ ^"brighten" ~ whitespace ~ int }
contrast = ${ ^"contrast" ~ whitespace ~ fp }
flip_horizontal = { ^"fliph" }
flip_vertical = { ^"flipv"  }
grayscale = { ^"grayscale" }
huerotate = ${ ^"huerotate" ~ whitespace ~ int }
invert = { ^"invert" }
resize = ${ ^"resize" ~ whitespace ~ uint ~ whitespace ~ uint }
rotate90 = { ^"rotate90" }
rotate180 = { ^"rotate180" }
rotate270 = { ^"rotate270" }


main = _{ soi ~ operation_sequence ~ eoi }