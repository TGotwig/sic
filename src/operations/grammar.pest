whitespace = _{ (" " | "\t" | "\n" | "\r\n")+ }
sep = _{ ";" }

fp = @{ int ~ ("." ~ ASCII_DIGIT+)? }
uint = @{ ASCII_DIGIT+ }
int  = @{ "-"? ~ ASCII_DIGIT+ }

triplet_sep = _{ whitespace ~ "|" ~ whitespace }
triplet_fp3 = _{ fp ~ whitespace ~ fp ~ whitespace ~ fp }

f3x3_args_sep = _{ triplet_fp3 ~ triplet_sep ~ triplet_fp3 ~ triplet_sep ~ triplet_fp3 }
f3x3_args_no_sep = _{ triplet_fp3 ~ whitespace ~ triplet_fp3  ~ whitespace ~ triplet_fp3 }


blur = ${ ^"blur" ~ whitespace ~ uint}
brighten = ${ ^"brighten" ~ whitespace ~ int }
contrast = ${ ^"contrast" ~ whitespace ~ fp }
filter3x3 = ${ ^"filter3x3" ~ whitespace ~ (f3x3_args_sep | f3x3_args_no_sep) }
flip_horizontal = { ^"fliph" }
flip_vertical = { ^"flipv"  }
grayscale = { ^"grayscale" }
huerotate = ${ ^"huerotate" ~ whitespace ~ int }
invert = { ^"invert" }
resize = ${ ^"resize" ~ whitespace ~ uint ~ whitespace ~ uint }
rotate90 = { ^"rotate90" }
rotate180 = { ^"rotate180" }
rotate270 = { ^"rotate270" }
unsharpen = ${ ^"unsharpen" ~ whitespace ~ fp ~ whitespace ~ int }


operation_sequence = _{ (operation ~ whitespace? ~ sep? ~ whitespace?)+ }

operation = _{ blur | brighten | contrast | filter3x3 | flip_horizontal | flip_vertical | grayscale | huerotate | invert | resize | rotate90 | rotate180 | rotate270 | unsharpen }

main = _{ SOI ~ operation_sequence ~ EOI }