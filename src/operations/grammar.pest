whitespace = _{ (" " | "\t" | "\n" | "\r\n") }
sep = _{ ";" }

digit = { '0'..'9' }

fp = @{ int ~ ("." ~ digit+)? }
uint = @{ digit+ }
int  = @{ "-"? ~ digit+ }

triplet_sep = _{ "|" }
triplet_f3 = ${ fp ~ whitespace ~ fp ~ whitespace ~ fp ~ whitespace ~ triplet_sep? }

blur = ${ ^"blur" ~ whitespace ~ uint}
brighten = ${ ^"brighten" ~ whitespace ~ int }
contrast = ${ ^"contrast" ~ whitespace ~ fp }
filter3x3 = ${ ^"filter3x3" ~ whitespace ~ triplet_f3 ~  triplet_f3 ~ triplet_f3 }
flip_horizontal = { ^"fliph" }
flip_vertical = { ^"flipv"  }
grayscale = { ^"grayscale" }
huerotate = ${ ^"huerotate" ~ whitespace ~ int }
invert = { ^"invert" }
resize = ${ ^"resize" ~ whitespace ~ uint ~ whitespace ~ uint }
rotate90 = { ^"rotate90" }
rotate180 = { ^"rotate180" }
rotate270 = { ^"rotate270" }
unsharpen = ${ ^"unsharpen" ~ whitespace ~ fp ~ whitespace ~ int }


operation_sequence = _{ (operation ~ sep?)+ }

operation = _{ blur | brighten | contrast | flip_horizontal | flip_vertical | grayscale | huerotate | invert | resize | rotate90 | rotate180 | rotate270 | unsharpen }

main = _{ soi ~ operation_sequence ~ eoi }
